plugins {
    id 'java-library'
    id 'fabric-loom' version '1.10-SNAPSHOT'
    id 'idea'
}

version = project.mod_version + "+fabric+mc" + project.minecraft_version
group = project.mod_group

base {
    archivesName = project.mod_id
}

repositories {
    maven {
        name = 'Fabric'
        url = 'https://maven.fabricmc.net/'
    }
    maven {
        name = 'Parchment'
        url = 'https://maven.parchmentmc.org/'
    }
    maven {
        name = "Cloth Config"
        url = "https://maven.shedaniel.me/"
    }
    maven {
        name = "Mod Menu"
        url = "https://maven.terraformersmc.com/releases/"
    }
    maven {
        name = "Mod Menu dependency"
        url = "https://maven.nucleoid.xyz/"
    }
}

fabricApi {
    configureDataGeneration {
        client = true
    }
}

dependencies {
    // To change the versions see the gradle.properties file.
    minecraft "com.mojang:minecraft:${project.minecraft_version}"
    // Use official mappings + parchment for parameter names.
    mappings loom.layered() {
        officialMojangMappings()
        parchment "org.parchmentmc.data:parchment-${project.parchment_mappings}@zip"
    }
    // Fabric loader.
    modImplementation "net.fabricmc:fabric-loader:${project.loader_version}"
    // Fabric API.
    modImplementation "net.fabricmc.fabric-api:fabric-api:${project.fabric_version}"
    // ModMenu and ClothConfig.
    modImplementation "com.terraformersmc:modmenu:${project.modmenu_version}"
    modImplementation "me.shedaniel.cloth:cloth-config-fabric:${project.cloth_config_version}"
}

idea {
    module {
        // Download sources/javadoc jars for dependencies.
        downloadSources = true
        downloadJavadoc = true
    }
}

processResources {
    inputs.property "version", project.version

    filesMatching("fabric.mod.json") {
        expand "mod_group": project.mod_group,
                "mod_id": project.mod_id,
                "mod_version": project.mod_version,
                "version": project.version,
                "minecraft_version": "~${project.minecraft_version}",
                "loader_version": ">=${project.loader_version}",
                "fabric_version": ">=${project.fabric_version}",
                "cloth_config_version": ">=${project.cloth_config_version}",
                "modmenu_version": ">=${project.modmenu_version}"
    }
}

tasks.withType(JavaCompile).configureEach {
    it.options.encoding = 'UTF-8'
    it.options.release = 21
}

java {
    sourceCompatibility = JavaVersion.VERSION_21
    targetCompatibility = JavaVersion.VERSION_21
    toolchain.languageVersion = JavaLanguageVersion.of(21)
}

jar {
    from("LICENSE")
    exclude(".cache")
}
